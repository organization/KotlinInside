import org.jmailen.gradle.kotlinter.tasks.*

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'org.jetbrains.dokka' version '1.7.20'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'signing'
    id 'org.jmailen.kotlinter' version '3.12.0'
    id 'com.google.protobuf' version '0.9.1'
}

boolean isDev = false

String libName = 'KotlinInside'
String libDevVersion = isDev ? '-SNAPSHOT' : ''
String libVersion = "1.14.6$libDevVersion"
String libDesc = 'Unofficial DCInside API written in Kotlin'

group = 'be.zvz'
version = libVersion

repositories {
    // Use mavenCentral for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

javadoc.options.encoding = 'UTF-8'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.9'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
                kotlin {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.5.0'


    //Jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.4'

    //FlexMark
    implementation group: 'com.vladsch.flexmark', name: 'flexmark', version: '0.62.2'
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-gfm-strikethrough', version: '0.62.2'
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-tables', version: '0.62.2'
    implementation group: 'com.vladsch.flexmark', name: 'flexmark-ext-gfm-tasklist', version: '0.62.2'

    // Protobuf
    implementation group: 'com.google.protobuf', name: 'protobuf-kotlin-lite', version: '3.21.8'

    // Use the Kotlin JDK standard library.
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk7'
    // Use the Kotlin coroutines library.
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.6.4'
    // Use the Kotlin test library.
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test'
    // Use the Kotlin JUnit integration.
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.1'
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

tasks.withType(Copy) {
    filesMatching("**/*.proto") {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

dokkaJekyll {
    outputDirectory.set(file("docs"))

    dokkaSourceSets {
        configureEach {
            platform.set(org.jetbrains.dokka.Platform.jvm)

            jdkVersion.set(7)
            noStdlibLink.set(false)
            noJdkLink.set(false)

            sourceRoots.setFrom(file("src/main/kotlin"))
            sourceRoots.from(file("src/main/java"))
        }
    }
}

// Maven-Central publish
task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task ktLint(type: LintTask, group: 'verification') {
    source files('src')
}

task ktFormat(type: FormatTask, group: 'formatting') {
    source files('src')
    report = file('build/format-report.txt')
    disabledRules = ["import-ordering"]
}

signing {
    useGpgCmd()
    sign publishing.publications
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

project.publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            artifactId = libName
            pom {
                name = libName
                description = 'Unofficial DCInside API written in Kotlin'
                url = 'https://github.com/organization/KotlinInside'
                inceptionYear = '2019'
                packaging = 'jar'
                groupId = 'be.zvz'
                artifactId = libName
                version = libVersion
                licenses {
                    license {
                        name = 'GNU General Public License 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'JellyBrick'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/organization/KotlinInside.git'
                    developerConnection = 'scm:git:ssh://git@github.com:organization/KotlinInside.git'
                    url = 'https://github.com/organization/KotlinInside.git'
                }
            }
        }
    }
}

project.nexusPublishing {
    packageGroup = group
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))

            def localProps = new Properties()
            File propFile = project.file('local.properties')
            if (propFile.exists()) {
                localProps.load(propFile.newDataInputStream())

                username = localProps.getProperty('centralUser')
                password = localProps.getProperty('centralPassword')
            }
        }
    }
}
